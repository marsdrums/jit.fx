<jittershader name="default">
	<description>Default Slab </description>
	<param name="tex0" type="int" default="0" />
	<param name="randTex" type="int" default="1" />
	<texture file="stbn_cos_scalar_tiled.png" type="float16" unit="1" rectangle="0" filter="none none" wrap="repeat repeat"/>	
	<param name="uv" type="vec2" state="TEXCOORD" />
	<param name="texMat" type="mat4" state="TEXTURE0_MATRIX" />
	<param name="blur_amount" type="float" default="1" />
	<param name="center" type="vec2" default="0. 0." />
	<param name="tile" type="vec2" default="0. 0." />
	<param name="mode" type="int" default="0" />
	<param name="texDim" type="vec2" state="TEXDIM0" />
	<language name="glsl" version="1.5">
		<bind param="tex0" program="fp" />
		<bind param="randTex" program="fp" />
		<bind param="uv" program="vp" />
		<bind param="texMat" program="vp" />
		<bind param="blur_amount" program="fp" />
		<bind param="center" program="fp" />
		<bind param="tile" program="fp" />
		<bind param="mode" program="fp" />
		<bind param="texDim" program="fp" />
		<program name="vp" type="vertex"  >
<![CDATA[
#version 330 core

in vec2 uv;
uniform mat4 texMat;

out jit_PerVertex {
	vec2 uv;
} jit_out;

void main(void) {
	gl_Position = vec4(uv*2 - 1, 0, 1);
	jit_out.uv = (texMat * vec4(uv,0,1)).xy;
}
]]>
</program>

<program name="fp" type="fragment"  >
<![CDATA[
#version 330 core


layout (location = 0) out vec4 outColor;

uniform sampler2DRect tex0, randTex;
uniform float blur_amount;
uniform vec2 center, tile, texDim;
uniform int mode;

in jit_PerVertex {
	vec2 uv;
} jit_in;

vec4 blur5(sampler2DRect image) {
  vec4 color = vec4(0.0);
  float randAmount = texelFetch(randTex, ivec2(mod(jit_in.uv, 128) + tile)).w*2;
  vec2 direction = normalize(jit_in.uv - center);
  vec2 off1 = vec2(1.3333333333333333) * direction * blur_amount * randAmount;
  color += texture(image, jit_in.uv) * 0.29411764705882354;
  color += texture(image, jit_in.uv + (off1)) * 0.35294117647058826;
  color += texture(image, jit_in.uv - (off1)) * 0.35294117647058826;
  return color; 
}

vec2 car2pol(vec2 cartesian) {
    float r = length(cartesian);           // r = sqrt(x^2 + y^2)
    float theta = atan(cartesian.y, cartesian.x);  // theta = atan(y, x)
    return vec2(r, theta);
}

vec2 pol2car(vec2 polar) {
    float x = polar.x * cos(polar.y);  // x = r * cos(theta)
    float y = polar.x * sin(polar.y);  // y = r * sin(theta)
    return vec2(x, y);
}

vec4 blur5_circle(sampler2DRect image) {
  vec4 color = vec4(0.0);
  vec2 pol = car2pol(jit_in.uv - center);
  float randAmount = texelFetch(randTex, ivec2(mod(jit_in.uv, 128) + tile)).w*0.005;
  float off1 = blur_amount * randAmount;
  color += texture(image, jit_in.uv) * 0.29411764705882354;
  color += texture(image, center + pol2car(vec2(pol.x, pol.y + off1))) * 0.35294117647058826;
  color += texture(image, center + pol2car(vec2(pol.x, pol.y - off1))) * 0.35294117647058826;
  return color; 
}

void main(void) {

	outColor = mode == 0 ? blur5(tex0) : blur5_circle(tex0);
}
]]>
</program>
</language>
</jittershader>
